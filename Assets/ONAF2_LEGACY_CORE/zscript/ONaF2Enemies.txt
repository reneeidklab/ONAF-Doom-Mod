// ZScript Monsters;

Class BBB2 : Actor
{
    //Variables.
    private int night;
    private int needstarget;
    private int aggro;
    
    override void PostBeginPlay()
    {
        needstarget = 0;
        aggro = 0;

        if (level.mapname == "GAME2N1")
        {
            night = 1;
        }
        else if (level.mapname == "GAME2N2")
        {
            night = 2;
        }

        Super.PostBeginPlay();
    }

    void A_BlamThink()
    {
        CustomSetTargetForce();
        
		if (target && target.health > 0)
		{
			if (!CheckSight(target, 0)) // Nao vejo o alvo...
			{
				aggro = 0;
				
				if (!CheckProximity("ThePlayer", 256.0, 1, 0, AAPTR_DEFAULT)) // Eu nao estou proximo de um jogador...
				{
					needstarget += 2+(night*4);
						
					if (needstarget > 700) // Eu nao me mexo ha bastante tempo...
					{
						needstarget = 0;
						SetStateLabel("WarpWalk");
					}
				}
				
				if (random(0, 4096) > 4093)
				{
					SetStateLabel("Wander");
				}
			}
			

			if (CheckSight(target, 0)) // Estou vendo um alvo...
			{
				needstarget = 0;
				
				if (CheckProximity("ThePlayer", 500.0, 1, 0, AAPTR_DEFAULT)) // Estou bem proximo de um jogador...
				{
					if (random(0, 1024) > 1021-(night*3)) // Alta chance de comecar a perseguir...
					{
						SetStateLabel("Walk");
					}
				}
				else // Nao estou proximo de um jogador, mas vejo um...
				{
					if (CheckIfInTargetLOS(90,0,0,0)) // O jogador esta longe, mas eu vejo ele e ele me ve.
					{
						aggro += 5+(night*2);
		
						if (aggro > 1600)
						{
							aggro = 0;
							SetStateLabel("RageRun");
						}
					}
				}
			}
		}
    }
    
    void A_BlamWalk()
    {
        needstarget = 0;
        aggro = 0;
        A_SetSpeed(5.5, AAPTR_DEFAULT);
        A_Chase("Melee", null);
        
        if (random(0, 1024) > 1023 && !CheckIfTargetInLOS(360,0,0,0)) // Chance de desistir de perseguir.
        {
            SetStateLabel("LoseTarget");
        }
    }

    void A_BlamRun()
    {
        needstarget = 0;
        aggro = 0;
        A_SetSpeed(10.0, AAPTR_DEFAULT);
        A_Chase("Melee", null);
        
        if (random(0, 2048) > 2047 && !CheckIfTargetInLOS(360,0,0,0)) // Chance de desistir de perseguir.
        {
            SetStateLabel("LoseTarget");
        }
    }

    void A_BlamWander()
    {
        aggro = 0;
        A_SetSpeed(3.5, AAPTR_DEFAULT);
        A_Wander;
        A_LookEx(0, 0, 0, 0, 360, "Walk");
    }

    void A_BlamWarpWalk()
    {
        needstarget = 0;
        aggro = 0;
        if (!CheckProximity("ThePlayer", 100.0, 1, 0, AAPTR_DEFAULT))
        {
            A_SetSpeed(18.0, AAPTR_DEFAULT);
            A_Chase(null, null);
        }
        else
        {
            SetStateLabel("WarpWalkEnd");
        }
    }
	
	void CustomSetTarget()
	{
		ThinkerIterator it = ThinkerIterator.Create("PlayerPawn");
		Actor plr;

		while ((plr = Actor(it.Next())) != null)
		{
			if (CheckFOV(plr, 90) && CheckSight(plr) && plr.health > 2)
			{
				Target = plr;
				return;
			}
		}
	}

	void CustomSetTargetForce()
	{
		ThinkerIterator it = ThinkerIterator.Create("PlayerPawn");
		Actor plr;

		while ((plr = Actor(it.Next())) != null)
		{
			if (plr.health > 2)
			{
				Target = plr;
				return;
			}
		}
	}

    Default
    {
        Health 2;
        Radius 15;
        Height 70;
        Mass 1000;
        Speed 3.5;
        Scale 2.7;
        PainChance 0;
        MeleeRange 50;
        CameraHeight 66;
        MaxStepHeight 500;
        MaxDropOffHeight 500;
        Species "Animatronic";
        Monster;
        +SOLID;
        +INVULNERABLE;
        +NODAMAGE;
        +DROPOFF;
        +INTERPOLATEANGLES;
        +NOPAIN;
        SeeSound "";
        PainSound "";
        DeathSound "";
        ActiveSound "";
        Obituary "%o met their fate to Birthday Boy Blam.";
    }

    States
    {
	Stage:
		CYBI A 1;
		Loop;
	Spawn:
		CYBI AAABBBCCCDDDEEEFFFGGGHHH 2 A_BlamThink();
		CYBI A 0 A_Jump(2, "Wander");
		Loop;
	Wander:
		CYBI JKLMNOPQRSTUVWXYZ 4 A_BlamWander();
		Loop;
	Walk:
		CYBI JKLMNOPQRSTUVWXYZ 2 A_BlamWalk();
		Loop;
	RageRun:
		CYBI JKLMNOPQRSTUVWXYZ 1 A_BlamRun();
		Loop;
	WarpWalk:
	WarpWalkLoop:
		CYBI JKLM 3 A_BlamWarpWalk();
		TNT1 AAAA 1 A_BlamWarpWalk();
		CYBI NOPQ 3 A_BlamWarpWalk();
		TNT1 AAAA 1 A_BlamWarpWalk();
		CYBI RSTUV 3 A_BlamWarpWalk();
		TNT1 AAAA 1 A_BlamWarpWalk();
		CYBI WXYZ 3 A_BlamWarpWalk();
		Loop;
	WarpWalkEnd:
		CYBI A 10 A_Stop();
		Goto RageRun;
	LoseTarget:
		CYBI A 1 A_Stop;
		CYBI A 1 A_ClearTarget;
		Goto Spawn;
	Melee:
		CYBI A 0 A_Stop;
		CYBI A 0 A_PlaySound("LargeScream", 0, 1.5);
		CYBI A 0 A_QuakeEx(1.5, 1.5, 1.5, 35, 0, 62, "", 0, 1, 1, 1, 62, 0, 0.5, 1, 1, 0, 0);
		CYBI A 0 ACS_ExecuteAlways(101, 0, 0, 0, 0);
		CYBA ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
		Goto LoseTarget;
    }
}