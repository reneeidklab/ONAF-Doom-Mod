// Manages the components of the beam, and attaches it to its source
class PowerFlashlight : Powerup
{
    Property EnergyType : energyType;
    Property FlickerThreshold : flickerThreshold;
    Property Offset : ofsX, ofsY, ofsZ;
    // Beam hotspot properties
    Property SpotColor : spotColor;
    Property SpotRange : spotRange;
    Property SpotInnerAngle : spotInnerAngle;
    Property SpotOuterAngle : spotOuterAngle;
    // Spill light properties
    Property SpillColor : spillColor;
    Property SpillRange : spillRange;
    Property SpillInnerAngle : spillInnerAngle;
    Property SpillOuterAngle : spillOuterAngle;
    // Reflected light properties
    Property BounceColor : bounceColor;
    Property BounceRange : bounceRange;
    Property BounceInnerAngle : bounceInnerAngle;
    Property BounceOuterAngle : bounceOuterAngle;
    Default
    {
        Inventory.MaxAmount 1;
        Powerup.Duration 0x7FFFFFFF;
        PowerFlashlight.EnergyType "FlashlightBattery";
        PowerFlashlight.FlickerThreshold 125;
        PowerFlashlight.Offset 3, 0, 0;
        PowerFlashlight.SpotColor 0xffffff;
        PowerFlashlight.SpotRange 256;
        PowerFlashlight.SpotInnerAngle 0;
        PowerFlashlight.SpotOuterAngle 25;
        PowerFlashlight.SpillColor 0x999999;
        PowerFlashlight.SpillRange 215;
        PowerFlashlight.SpillInnerAngle 0;
        PowerFlashlight.SpillOuterAngle 30;
        PowerFlashlight.BounceColor 0x888888;
        PowerFlashlight.BounceRange 0;
        PowerFlashlight.BounceInnerAngle 0;
        PowerFlashlight.BounceOuterAngle 45;
    }
    class<Inventory> energyType;
    double flickerThreshold;
    double ofsX, ofsY, ofsZ;
    Color spotColor;
    int spotRange;
    double spotInnerAngle;
    double spotOuterAngle;
    Color spillColor;
    int spillRange;
    double spillInnerAngle;
    double spillOuterAngle;
    Color bounceColor;
    int bounceRange;
    double bounceInnerAngle;
    double bounceOuterAngle;
    FlashlightBeam spot;
    FlashlightBeam spill;
    FlashlightBounce bounce;
    override void AttachToOwner(Actor other)
    {
        Super.AttachToOwner(other);
        spot = FlashlightBeam(Spawn("FlashlightBeam", other.pos));
        spot.master = other;
        spot.offset = (ofsX, ofsY, ofsZ);
        spot.args[0] = spotColor.r;
        spot.args[1] = spotColor.g;
        spot.args[2] = spotColor.b;
        spot.args[3] = spotRange;
        spot.spotInnerAngle = spotInnerAngle;
        spot.spotOuterAngle = spotOuterAngle;
        if (spillRange > 0)
        {
            spill = FlashlightBeam(Spawn("FlashlightBeam", other.pos));
            spill.master = other;
            spill.offset = (ofsX, ofsY, ofsZ);
            spill.args[0] = spillColor.r;
            spill.args[1] = spillColor.g;
            spill.args[2] = spillColor.b;
            spill.args[3] = spillRange;
            spill.spotInnerAngle = spillInnerAngle;
            spill.spotOuterAngle = spillOuterAngle;
        }
        if (bounceRange > 0)
        {
            bounce = FlashlightBounce(Spawn("FlashlightBounce", other.pos));
            bounce.master = other;
            bounce.args[0] = bounceColor.r;
            bounce.args[1] = bounceColor.g;
            bounce.args[2] = bounceColor.b;
            bounce.args[3] = bounceRange;
            bounce.spotInnerAngle = bounceInnerAngle;
            bounce.spotOuterAngle = bounceOuterAngle;
        }
    }

    override void DoEffect()
    {
        Super.DoEffect();
        int energy = energyType ? owner.CountInv(energyType) : effectTics;
        int flicker = Random(0, flickerThreshold);
        if (flicker < 0) flicker *= -ticRate;
        if (flicker > energy)
        {
            spot.args[3] = 0;
            if (spill) spill.args[3] = 0;
            if (bounce) bounce.args[3] = 0;
        }
        else
        {
            spot.args[3] = spotRange;
            if (spill) spill.args[3] = spillRange;
            if (bounce) bounce.args[3] = bounceRange;
        }
    }
    override void DetachFromOwner()
    {
        spot.Destroy();
        if (spill) spill.Destroy();
        if (bounce) bounce.Destroy();
        Super.DetachFromOwner();
    }
}

class FlashlightBeam : SpotLight
{
    Vector3 offset;
    override void Tick()
    {
        Super.Tick();
        Vector3 beamPos = master.pos;
        beamPos.xy += AngleToVector(master.angle, offset.x);
        beamPos.xy += AngleToVector(master.angle - 90, offset.y);
        let plyr = PlayerPawn(master);
        if (plyr && plyr.player) { beamPos.z += (plyr.height / 2 + plyr.attackZOffset) * plyr.player.crouchFactor; }
        else { beamPos.z += master.height / 2; }
        SetOrigin(beamPos, true);
        vel = master.vel;
        angle = master.angle;
        pitch = master.pitch;
    }
}

class FlashlightBounce : SpotLight
{
    Vector3 offset;

    override void Tick()
    {
        Super.Tick();

        // Find intersection point
        double ofsZ;
        let plyr = PlayerPawn(master);
        if (plyr && plyr.player) { ofsZ += (plyr.height / 2 + plyr.attackZOffset) * plyr.player.crouchFactor; }
        else { ofsZ += master.height / 2; }
        FLineTraceData data;
        master.LineTrace( master.angle, 8192, master.pitch, TRF_ThruActors | TRF_NoSky, ofsz, offset.x, offset.y, data);
        // Bounce light comes from intersection
        Vector3 bouncePos = data.hitLocation;
        bouncePos.xy += AngleToVector(master.angle + 180);
        SetOrigin(bouncePos, true);
        // Find reflection vector
        Vector3 normal;
        if (data.hitType == Trace_HitWall)
        {
            if (data.lineSide = Line.front) normal.xy = RotateVector(data.hitLine.delta.Unit(), -90);
            else normal.xy = RotateVector(data.hitLine.delta.Unit(), 90);
        }
        else if (data.hitType == Trace_HitFloor) { normal = data.hitSector.floorPlane.Normal; }
        else if (data.hitType == Trace_HitFloor) { normal = data.hitSector.ceilingPlane.Normal; }
        else { Deactivate(null); return; }
        Activate(null);
        Vector3 reflection = data.hitDir - 2 * (data.hitDir dot normal) * normal;
        angle = VectorAngle(reflection.x, reflection.y);
        pitch = -ASin(reflection.z / reflection.Length());
    }
}

Class FlashlightBattery : Ammo
{
	Default
	{
		Inventory.PickupMessage "Picked up an battery";
		Inventory.Amount 999;
		Inventory.MaxAmount 999;
		Ammo.BackpackAmount 150;
		Ammo.BackpackMaxAmount 999;
		Inventory.Icon "CELLA0";
	}
	States
	{
	Spawn:
		CELL A -1;
		Stop;
	}
}

class Flashlight : PlasmaRifle
{
	override void Tick()
	{
		super.Tick();
		if (owner == null)
			return;
		If (owner.CheckInventory("IsZoey",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Zoey","ONAFZoeyFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsLouis",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Louis","ONAFLouisFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsBill",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Bill","ONAFBillFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsFrancis",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Francis","ONAFFrancisFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsNick",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Nick","ONAFNickFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsEllis",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Ellis","ONAFEllisFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsCoach",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Coach","ONAFCoachFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsRochelle",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Rochelle","ONAFRochelleFlashlight.iqm"); }
		Else if (owner.CheckInventory("IsSmith",1,AAPTR_DEFAULT)) { self.A_ChangeModel("",0,"Models/Characters/Smith","ONAFSmithFlashlight.iqm"); }
		
		if (owner.CheckInventory("FlashlightStop",1,AAPTR_DEFAULT)) { self.SetAnimation('Idle', framerate:17.5, interpolatetics:8, flags:SAF_LOOP|SAF_NOOVERRIDE); }
		else if (owner.CheckInventory("FlashlightWalk",1,AAPTR_DEFAULT)) { self.SetAnimation('WalkFoward', framerate:35, interpolatetics:4, flags:SAF_LOOP|SAF_NOOVERRIDE); }
		else if (owner.CheckInventory("FlashlightRun",1,AAPTR_DEFAULT)) { self.SetAnimation('RunFoward', framerate:35, interpolatetics:2, flags:SAF_LOOP|SAF_NOOVERRIDE); }
	}
	Default
	{
		Weapon.BobStyle "Smooth";
		Weapon.BobRangeY 1.0;
		Weapon.BobRangeX 0.5;
		Weapon.BobSpeed 2.0;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 999;
		Weapon.AmmoType "FlashlightBattery";
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 999;
		RenderStyle "Normal";
		Alpha 1.0;
		+DECOUPLEDANIMATIONS
		+WEAPON.NOALERT;
		+WEAPON.NOAUTOAIM;
		+WEAPON.NOAUTOFIRE;
		Obituary "%o chewed on %k's ...flash...light... ...what?";
	}
	States
	{
	Ready: 
		CYBI A 1
		{
			If (CountInv("PowerFlashlight")) { A_FireCustomMissile ("FlashLightLight", 0, 0, 0, 0); }
			A_WeaponReady();
			A_AlertMonsters();
		}
		Loop;
	Ready3: 
		CYBI A 0 A_TakeInventory("PowerFlashlight",1);
		Goto Ready;
	Deselect: 
		CYBI AAAAAAAAAAAAAAA 0 A_Lower();
		CYBI A 1 A_Lower();
		Goto Deselect;
	Select:
		CYBI A 0 A_WeaponReady();
		CYBI AAAAAA 1;
		Goto Ready;
	Fire:
		CYBI A 1
		{
			A_PlaySound("LightsOn",0,1.0);
			if (CountInv("PowerFlashlight")) { A_TakeInventory("PowerFlashlight"); }
			else { A_GiveInventory("PowerFlashlight"); }
		}
		Goto Ready;
	}
}

CLASS FlashLightLight : FastProjectile
{
	Default
	{
	  Speed 198;
	  Radius 8;
	  Height 8;
	  Damage 0;
	  DamageType "Light";
	  +BLOODLESSIMPACT;
	  +FORCEPAIN;
	  Projectile;
	  MissileType "FlashLightLight2";
	  SeeSound "0";
	  DeathSound "0";
	  }
	  States
  {
  Spawn:
    TNT1 A 1;
    BLNK A 1;
    BLNK A 1;
    Stop;
  Death:
	BLNK AA 1;
    Stop;
  }
}

CLASS FlashLightLight2 : Actor
{
  Default
  {
  Radius 1;
  Height 1;
  +NOBLOCKMAP;
  SeeSound "0";
  DeathSound "0";
  }
  States
  {
  Spawn:
	TNT1 A 1;
    stop;
  }
}

class Empty : Fist
{
	Default
	{
	Inventory.Amount 1;
	Inventory.MaxAmount 1;
	Inventory.UseSound "";
	Inventory.PickupSound "";
	Inventory.PickupMessage "";
	}
	
	States
	{
	Select:
		TNT1 A 1;
		Goto Ready;
	Ready:
		TNT1 A 1;
		Loop;
	}
}